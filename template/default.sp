******************************** STL Designer 2 T element Template ver 2.3 ********************************

*** Notice *** ------------------------------------------------------*
* STLのTエレメントの基本形テンプレート
* このファイル（default.sp）は basic/basic_T.sp と同じ内容です．コメントのみ異なります．
* STLのセグメント数は2以上の整数でなければ動きません（1以下は不可）
* ノード（観測点等のつなぎ目）には数字か pt を含む文字列のみとします（exp: \w*pt\d*）
* ***hoge*** ---* はただのコメントではなく，STLDesigner2にとって文法的な意味を持ちますので注意してください．
* *---* はただのコメントではなく，STLDesigner2にとって文法的な意味を持ちますので注意してください．

*** PRBSの使い方
* 凡例：
* LFSR(L[V], H[V], delay, TR, TF, bitrate, seed, tap(ADS_default = [8,4,3,2])
* Vs1	101	0 LFSR( 0 2 200p 200p 200p 500meg 3 [8,4,3,2])


*** サンプリング電圧のインクルードのやり方
* 改行位置などに注意が必要です（.INCLUDE が行頭に来る必要あり）
* Vs1	101	0 PWL(
* .INCLUDE ./data/500Mbps-3.3V-koritsuha-2 )

*--------------------------------------------------------------------*


*** STL Circuit *** -------------------------------------------------*

Vs1             101     0       PULSE( 0 2 1n 200p 200p 1.8n 4n )
Rin1            101     102     50

* HSPICEで行途中のコメント $ 以降の記述（Tエレメントの "$ LEN=100mm" ）は，HSPICEでは無視されますが，
* STLデザイナー2で利用します．
T1_STL_3        102     0       optpt1  0       Z0=50   TD=630.8p       $ LEN=100m
C1              optpt1  0       10p
T2_STL_8        optpt1  0       optpt2  0       Z0=50   TD=630.8p       $ LEN=100m
C2              optpt2  0       10p
T3_STL_3        optpt2  0       optpt3  0       Z0=50   TD=630.8p       $ LEN=100m

RT1             optpt3  0       50

*--------------------------------------------------------------------*


*** Versus Circuit *** ----------------------------------------------*

* STLデザイナー2で分割したセグメントのノード番号には，
* サブスペース1では1100, 1101, ... (セグメント数分連番) ， サブスペース2では 2100, 2101, ...（セグメント数分連番）
* のようにノード番号が割り振られます．テンプレートの中でこれらの番号と被らないように気を付けてください．
Vsvs1          1001    0       PULSE( 0 2 1n 200p 200p 1.8n 4n )
Rinvs1         1001    1002    50
Tvs1           1002    0       vspt1   0       z0=50   TD=630.8p
Tvs2           vspt1   0       vspt2   0       z0=50   TD=630.8p
Tvs3           vspt2   0       vspt3   0       z0=50   TD=630.8p
RTvs1          vspt3   0       50

*--------------------------------------------------------------------*


*** Trigger Circuit *** ---------------------------------------------*
* アイパターン用のトリガーを記述します。
* 本テンプレートでは使用しません。


*--------------------------------------------------------------------*


*** Netlist Commands *** --------------------------------------------*

* プリントアウト幅（80 or 132）: .lisファイルの横幅文字列を指定します．80か132しか意味がありません（マニュアルより）
.WIDTH OUT=132

* .TRAN resolution end_time start_time（これ以外の記述方法は不可）
* HSPICEでは .TRAN 50p 40n str=20n という書き方が推奨されていますが，これはSTLデザイナー2では使えません．
.TRAN 50p 40n 20n

* .PRINT で.lisファイルに出力する波形の観測点を指定します．
* 順序は何でも大丈夫です．
.PRINT v(optpt1) v(optpt2) v(optpt3) v(vspt1) v(vspt2) v(vspt3)

* HSPICEに記述の終わりを知らせるコマンドです．
.END

*--------------------------------------------------------------------*

* 以降，STL候補のネットリストファイル（.sp）では，スコアが記入されます．
* テンプレートのネットリストにはスコアは記入されません．
